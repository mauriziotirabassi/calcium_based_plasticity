neuron hill_tononi_neuron: 
    state:
        r integer = 0      # counts number of tick during the refractory period
        g_spike boolean = true
        V_m mV = ( g_NaL * E_Na + g_KL * E_K ) / ( g_NaL + g_KL )
        Theta mV = Theta_eq 
        IKNa_D nS = 0.0 nS

        # Synaptic conductances
        g_AMPA real = 0
        g_NMDA real = 0
        g_AMPA$ real = AMPAInitialValue
        g_NMDA$ real = NMDAInitialValue
        m_VDCC real = m_VDCC_initialvalue
        h_VDCC real = h_VDCC_initialvalue
        
        # Synaptic efficacy
        Ca_in mol/um**3 = Ca_rest [[Ca_in > 0 mol/um**3]] # Intracellular calcium concentration
        cstar mol/um**3 = 0 mol/um**3
        p real = 0

    equations:
        # Synaptic conductance kernels
        kernel g_AMPA' = g_AMPA$ - g_AMPA  / AMPA_Tau_2,
               g_AMPA$' = -g_AMPA$ / AMPA_Tau_1

        kernel g_NMDA' = g_NMDA$ - g_NMDA / NMDA_Tau_2,
               g_NMDA$' = -g_NMDA$ / NMDA_Tau_1
               
        kernel g_exc = (e/tau_syn_exc) * t * exp(-t/tau_syn_exc)
        
        # Excitation current
        inline I_syn_exc pA = -convolve(g_exc, exc_spikes) * nS * ( V_m - E_exc )

        # VDCC conductance
        inline g_VDCC real = gca_ba_VDCC * m_VDCC**2 * h_VDCC
        inline m_inf_VDCC real = 1 / ( 1 + exp( ( ( VDCC_V_hm - VDCC_ljp ) - V_m ) / VDCC_k_m ) )
        inline h_inf_VDCC real = 1 / ( 1 + exp( ( ( VDCC_V_hh - VDCC_ljp ) - V_m ) / VDCC_k_h ) )
        m_VDCC' = ( m_inf_VDCC - m_VDCC ) / ( VDCC_tau_m * 1e3 )
        h_VDCC' = ( h_inf_VDCC - h_VDCC ) / ( VDCC_tau_h * 1e3 )

        # VDCC current
        inline log_approx real = (Ca_out - Ca_in) / max(Ca_in, Ca_rest)
        inline VDCC_E_rev mV = ( -( Rgas * Temp / ( 2 * Farado ) ) * log_approx ) * 10**(-3) #qui ln cao cai
        recordable inline I_VDCC pA = -g_VDCC * nS * ( V_m - VDCC_E_rev )

        # AMPA current
        recordable inline I_AMPA pA = -convolve(g_AMPA, AMPA) * nS * ( V_m - AMPA_E_rev )

        # NMDA current
        recordable inline I_NMDA pA = -convolve(g_NMDA, NMDA) * nS * ( V_m - NMDA_E_rev ) / ( 1 + exp( ( NMDA_Vact - V_m ) / NMDA_Sact ) )
         
        # Internal currents
        inline I_Na pA = -g_NaL * ( V_m - E_Na )
        inline I_K pA = -g_KL * ( V_m - E_K )

        # I_Na(p), m_inf^3 according to Compte et al, J Neurophysiol 2003 89:2707
        inline INaP_thresh mV = -55.7 mV
        inline INaP_slope mV = 7.7 mV
        inline m_inf_NaP real = 1.0 / ( 1.0 + exp( -( V_m - INaP_thresh ) / INaP_slope ) )
        inline I_NaP pA = -NaP_g_peak * m_inf_NaP**3 * ( V_m - NaP_E_rev )
        inline d_half real = 0.25
        inline m_inf_KNa real = 1.0 / ( 1.0 + ( d_half / ( IKNa_D / nS ) )**3.5 )
        inline I_KNa pA = -KNa_g_peak * m_inf_KNa * ( V_m - KNa_E_rev )
        inline D_influx_peak real = 0.025
        inline tau_D real = 1250.0 # yes, 1.25 s
        inline D_thresh mV = -10.0 mV
        inline D_slope mV = 5.0 mV
        inline D_influx real = 1.0 / ( 1.0 + exp( -( V_m - D_thresh ) / D_slope ) )
        IKNa_D' = ( D_influx_peak * D_influx * nS - ( IKNa_D  - KNa_D_EQ / mV ) / tau_D ) / ms
        
        # Spike current
        inline I_spike mV = (g_spike) ? -( V_m - E_K ) / Tau_spike * ms : 0 mV #state of g_spike is important HERE
        
        # Calcium dynamics
        Ca_in' = ( 1e-9 ) * ( I_NMDA + I_VDCC ) * Ca_eta / ( ( 1e-15 ) * Ca_X_volume * 2 * Farado ) - ( Ca_in - Ca_rest ) / Ca_tau
        
        # Membrane dynamics
        V_m'  = ( ( I_Na + I_K + I_AMPA + I_NMDA + I_NaP + I_VDCC + I_KNa + I_syn_exc + I_stim ) / Tau_m + I_spike * pA/(ms * mV) ) * s/nF
        
        # Synaptic efficacy dynamics
        p' = (-p * (1 - p) * (0.5 - p) + p_rate * (1 - p) * Hfun(cstar, th_p) - d_rate * p * Hfun(cstar, th_d)) / tau_syn
        cstar' = - ( cstar / tau_star ) + ( Ca_in - Ca_rest ) / ( 1 * ms )
                 
    parameters:
        # Neuron parameters
        E_exc mV = 0 mV
        E_Ca mV = 40.0 mV
        E_Rest mV = -65 mV
        E_Na mV = 30.0 mV
        E_K mV = -90.0 mV
        g_NaL nS =  0.2 nS
        g_KL nS = 1.0 nS            # 1.0 - 1.85
        Tau_m ms = 16.0 ms          # membrane time constant applying to all currents but repolarizing K-current (see [1, p 1677])
        Theta_eq mV = -51.0 mV      # equilibrium value
        Tau_spike ms = 1.75 ms      # membrane time constant applying to repolarizing K-current
        t_spike ms = 2.0 ms         # duration of re-polarizing potassium current
        t_ref ms = 700 ms           # added from iaf for refracrariness not ode
        tau_syn_exc ms = 0.2 ms

        # Parameters for synapse of type AMPA, GABA_A, GABA_B and NMDA
        AMPA_g_peak nS = 0.1 nS      # peak conductance
        AMPA_E_rev mV = 0.0 mV       # reversal potential
        AMPA_Tau_1 ms = 0.5 ms       # rise time
        AMPA_Tau_2 ms = 2.4 ms       # decay time, Tau_1 < Tau_2
        NMDA_g_peak nS = 0.075 nS    # peak conductance
        NMDA_Tau_1 ms = 4.0 ms       # rise time
        NMDA_Tau_2 ms = 40.0 ms      # decay time, Tau_1 < Tau_2
        NMDA_E_rev mV = 0.0 mV       # reversal potential
        NMDA_Vact mV = -58.0 mV      # inactive for V << Vact, inflection of sigmoid
        NMDA_Sact mV = 2.5 mV        # scale of inactivation
       
        # parameters for intrinsic currents
        NaP_g_peak nS = 1.0 nS       # peak conductance for intrinsic current
        NaP_E_rev mV = 30.0 mV       # reversal potential for intrinsic current
        KNa_g_peak nS = 1.0 nS       # peak conductance for intrinsic current
        KNa_E_rev mV = -90.0 mV      # reversal potential for intrinsic current
        KNa_D_EQ pA = 0.001 pA
        
        # VDCC parameters
        VDCC_ljp mV = 0 mV
        VDCC_g_bar nS/um**2 = 0.0744 nS/um**2 # Density spines: 20 um-2 (Sabatini 2000), unitary conductance VGCC 3.72 pS (Bartol 2015)
        VDCC_V_hm mV = -5.9 mV                # Half-maximum activation voltage for VDCC, Magee and Johnston 1995 (corrected for m*m)
        VDCC_k_m mV = 9.5 mV                  # Slope factor for VDCC activation, Magee and Johnston 1995 (corrected for m*m)
        VDCC_tau_m ms = 1 ms                  # Time constant activation variable m, TODO: It's a guess
        VDCC_V_hh mV = -39 mV                 # Half-maximum inactivation voltage for VDCC, Magee and Johnston 1995
        VDCC_k_h mV = -9.2 mV                 # Slope factor for VDCC inactivation, Magee and Johnston 1995
        VDCC_tau_h ms = 27 ms                 # Time constant inactivation variable h

        # Calcium parameters
        Ca_tau ms = 12 ms                       # Time constant of calcium transients Sabatini et al.
        Ca_eta real = 0.04                      # Percent of free calcium (not buffered), Sabatini et al 2002: kappa_e = 24+-11 (also 14 (2-31) or 22 (18-33))
        Ca_rest mol/um**3 = 7e-26 mol/um**3     # Sabatini et al 2002: 70+-29 nM, per AP: 1.1 (0.6-8.2) uM = 1100 e-6 mM = 1100 nM
        Ca_out mol/um**3 = 2e-24 mol/um**3      # Extracellular calcium concentration in slices
        Ca_X_volume um**3 = 0.087 um**3         # Spine volume
        
        # Thermodynamic constants
        Rgas J/(mol*K) = 8.314 J/(mol*K)
        Temp K = 310.0 K
        Farado C/mol = 96485.0 C/mol
        
        # Synaptic efficacy parameters
        tau_star ms = 278.318 ms
        tau_syn ms = 70000.0 ms
        th_p real = 0.000073            # Soglia per potenziamento
        th_d real = 0.000000072         # Soglia per depressione
        p_rate real = 216.2             # Tasso di potenziamento
        d_rate real = 101.5   
        
        # Constant external input current
        I_e pA = 0 pA

    internals:
        # Inline calculations for each synapse
        RefractoryCounts integer = steps(t_ref) # refractory time in steps

        # AMPA initialization
        inline exact_integration_adjustment_AMPA real = ( ( 1 / AMPA_Tau_2 ) - ( 1 / AMPA_Tau_1 ) ) * ms
        inline t_peak_AMPA ms = ( AMPA_Tau_2 * AMPA_Tau_1 ) * ln( AMPA_Tau_2 / AMPA_Tau_1 ) / ( AMPA_Tau_2 - AMPA_Tau_1 )
        inline normalisation_factor_AMPA real = 1 / ( exp( -t_peak_AMPA / AMPA_Tau_1 ) - exp( -t_peak_AMPA / AMPA_Tau_2 ) )
        inline AMPAInitialValue real = AMPA_g_peak * normalisation_factor_AMPA * exact_integration_adjustment_AMPA
        
        # NMDA initialization
        inline exact_integration_adjustment_NMDA real = ( ( 1 / NMDA_Tau_2 ) - ( 1 / NMDA_Tau_1 ) ) * ms
        inline t_peak_NMDA ms = ( NMDA_Tau_2 * NMDA_Tau_1 ) * ln( NMDA_Tau_2 / NMDA_Tau_1 ) / ( NMDA_Tau_2 - NMDA_Tau_1 )
        inline normalisation_factor_NMDA real = 1 / ( exp( -t_peak_NMDA / NMDA_Tau_1 ) - exp( -t_peak_NMDA / NMDA_Tau_2 ) )
        inline NMDAInitialValue real = NMDA_g_peak * normalisation_factor_NMDA * exact_integration_adjustment_NMDA
        
        # VDCC initialization
        inline gca_ba_VDCC real = ( VDCC_g_bar * 4 * 3.14 * ( ( 3 * Ca_X_volume / ( 4 * 3.14 ) ) )**(2/3) ) / nS
        inline m_VDCC_initialvalue real = 1 / ( 1 + exp( ( ( VDCC_V_hm - VDCC_ljp ) - E_Rest ) / VDCC_k_m ) )
        inline h_VDCC_initialvalue real = 1 / ( 1 + exp( ( ( VDCC_V_hh - VDCC_ljp ) - E_Rest ) / VDCC_k_h ) )
        
    input:
        AMPA <- spike
        NMDA <- spike
        exc_spikes <-spike
        I_stim pA <- continuous

    output:
        spike

    update:
        Ca_in = max(Ca_in, Ca_rest)
        integrate_odes()
        Ca_in = max(Ca_in, Ca_rest)  # Prevent Ca_in from dropping below Ca_rest
       
        if r > 0: # neuron is absolute refractory:
            g_spike = false  # Deactivate potassium current.
            r =  r - 1
        elif V_m >= Theta:  # neuron is not absolute refractory
            g_spike = true
            emit_spike()
            r = RefractoryCounts
           
    function Hfun(cStel real, th real) real:
        if cStel > th:
            return 1.0
        else:
            return 0.0
