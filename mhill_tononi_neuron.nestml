
neuron mhill_tononi_neuron: 
    state:
        r integer = 0           # Counts number of tick during the refractory period
        g_spike boolean = false 
        loga real = 10.26
        cstel real = 0
        AMPA_g_peak nS = 0.8 nS
        g_bool boolean = false
        V_m mV = ( g_NaL * E_Na + g_KL * E_K ) / ( g_NaL + g_KL )   # Baseline value for Vm 
        Theta mV = Theta_eq 
        
        # Synaptic conductances
        g_AMPA real = 0
        g_NMDA real = 0
        g_AMPA$ real = AMPAInitialValue
        g_NMDA$ real = NMDAInitialValue
        m_VDCC real = m_VDCC_initialvalue
        h_VDCC real = h_VDCC_initialvalue

        # Synaptic efficacy
        cai_CR mol/um**3 = min_ca_CR [[cai_CR > 0 mol/um**3]]
        cstar mol/um**3 = 0 mol/um**3
        p real = 0

    equations:
        #############
        # V_m
        #############
        
        # Artifical excitation
        inline I_syn_exc pA = convolve(g_exc, I_EXC) * nS * ( V_m - E_Na )

        # VDCC current
        recordable inline gca_VDCC real = gca_ba_VDCC * m_VDCC**2 * h_VDCC
        recordable inline Eca_syn mV = ( ( ( Rgas * Temp / ( 2 * Farado ) ) * loga ) )  #* 10**(-3) ) + 130 * mV  
        recordable inline I_Ca_vdcc pA = ( gca_VDCC * nS * ( V_m - Eca_syn ) ) / 10
 
        # AMPA and NMDA currents
        inline I_syn_ampa pA = AMPA_g_peak * convolve(g_AMPA, AMPA) * ( V_m - AMPA_E_rev ) 
        recordable inline I_syn_nmda pA = convolve(g_NMDA, NMDA) * nS * ( V_m - Eca_syn_nmda ) / ( 1 + exp( ( NMDA_Vact - V_m ) / NMDA_Sact ) ) 
        inline I_syn pA = I_syn_ampa + I_syn_nmda

        # Sodium and potassium currents
        inline I_Na pA = g_NaL * ( V_m - E_Na )
        inline I_K pA = g_KL * ( V_m - E_K )

        # AMPA conductance
        inline g_AMPA_bar nS = g_dep + p * ( g_pot - g_dep )
        
        # Potassium repolarizing current
        recordable inline I_spike mV = (g_spike) ? -( V_m - E_K ) / Tau_spike * ms : 0 mV
        
        #############
        #VDCC
        #############
        inline minf_VDCC real = 1 / ( 1 + exp( ( ( vhm_VDCC - ljp_VDCC ) - V_m ) / km_VDCC ) )
        inline hinf_VDCC real = 1 / ( 1 + exp( ( ( vhh_VDCC - ljp_VDCC ) - V_m ) / kh_VDCC ) )
        
        # VDCC gating variables dynamics
        m_VDCC' = ( minf_VDCC - m_VDCC ) / ( mtau_VDCC )
        h_VDCC' = ( hinf_VDCC - h_VDCC ) / ( htau_VDCC )
        
        # AMPA peak conductance dynamics
        AMPA_g_peak' = ( g_AMPA_bar - AMPA_g_peak ) / tau_ch
        
        # Intracellular calcium concentration dynamics
        cai_CR' = ( 10**(-20) * ( -I_syn_nmda - I_Ca_vdcc ) * gamma_ca_CR ) / ( volume_CR * 2 * Farado ) - ( cai_CR - min_ca_CR ) / tau_ca_CR
        
        # Membrane potential dynamics
        V_m'  = ( ( - I_Na - I_K - I_syn - I_syn_exc - I_Ca_vdcc + I_stim ) / Tau_m + I_spike * pA/(ms * mV) ) * s/nF  #I_KNa I_NaP
        
        # Synaptic efficacy dynamics
        p' = (-p * (1 - p) * (0.5 - p) + p_rate * (1 - p) * Hfun(cstar, th_p) - d_rate * p * Hfun(cstar, th_d)) / tau_syn
        
        # Leaky calcium integrator dynamics
        cstar' =  -( cstar / tau_star ) + ( cai_CR - min_ca_CR ) / ( 1 * ms )
        
        Theta' = -( Theta - Theta_eq ) / Tau_theta
        
        #############
        # Synapses
        #############
        kernel g_AMPA' = g_AMPA$ - g_AMPA  / AMPA_Tau_2,
               g_AMPA$' = -g_AMPA$ / AMPA_Tau_1

        kernel g_NMDA' = g_NMDA$ - g_NMDA / NMDA_Tau_2,
               g_NMDA$' = -g_NMDA$ / NMDA_Tau_1
               
        kernel g_exc = (e/tau_syn_exc) * t * exp(-t/tau_syn_exc)
               

    parameters:
        # Neuron parameters
        Eca_syn_nmda mV = 40 mV
        E_exc mV = 0 mV                 # Excitatory reversal potential
        E_Rest mV = -65 mV
        E_Ca mV = 40.0 mV
        E_Na mV = 30.0 mV #30
        E_K mV = -90.0 mV
        g_NaL nS =  0.2 nS
        g_KL nS = 1.0 nS                # 1.0 - 1.85
        Tau_m ms = 16.0 ms              # membrane time constant applying to all currents but repolarizing K-current (see [1, p 1677]) 16ms
        Theta_eq mV = -40.0 mV          # equilibrium value
        Tau_spike ms = 1.75 ms          # membrane time constant applying to repolarizing K-current 1.75
        t_ref ms = 20 ms
        Tau_theta ms = 30.0 ms  # time constant
        tau_syn_exc ms = 0.2 ms         # Synaptic time constant of excitatory synapse
        tau_ch ms = 8000 ms

        # AMPA parameters
        AMPA_E_rev mV = 0.0 mV          # reversal potential
        AMPA_Tau_1 ms = 0.5 ms          # rise time 0.5
        AMPA_Tau_2 ms = 2.4 ms          # decay time, Tau_1 < Tau_2 2.4

        # NMDA parameters
        NMDA_g_peak nS = 0.075 nS       # peak conductance
        NMDA_Tau_1 ms = 4.0 ms          # rise time 4
        NMDA_Tau_2 ms = 40.0 ms         # decay time, Tau_1 < Tau_2 40
        NMDA_E_rev mV = 0.0 mV          # reversal potential
        NMDA_Vact mV = -58.0 mV         # inactive for V << Vact, inflection of sigmoid
        NMDA_Sact mV = 2.5 mV           # scale of inactivation
       
        # VDCC parameters
        
        volume_CR um**3 = 0.087 um**3   # Spine volume
        gca_bar_VDCC nS/um**2 = 0.0744 nS/um**2 # Density spines: 20 um-2 (Sabatini 2000), unitary conductance VGCC 3.72 pS (Bartol 2015)
        ljp_VDCC mV = 0 mV
        vhm_VDCC mV = -5.9 mV           # Half-maximum activation voltage for VDCC, Magee and Johnston 1995 (corrected for m*m)
        km_VDCC mV = 9.5 mV             # Slope factor for VDCC activation, Magee and Johnston 1995 (corrected for m*m)
        vhh_VDCC mV = -39 mV            # Half-maximum inactivation voltage for VDCC, Magee and Johnston 1995
        kh_VDCC mV = -9.2 mV            # Slope factor for VDCC inactivation, Magee and Johnston 1995
        mtau_VDCC ms = 1 ms             # Time constant activation variable m (guess)
        htau_VDCC ms = 27 ms            # Time constant inactivation variable h
        
        # Calcium parameters
        CaBoh mol/um**3 = 0.000000000000000120 mol/um**3
        tau_ca_CR s = 0.012 s #0.012    # Time constant of calcium transients Sabatini et al.
        gamma_ca_CR real = 0.04
        min_ca_CR mol/um**3 = 0.00000000000000000000007 mol/um**3
        cao_CR mol/um**3 = 0.000000000000000002 mol/um**3
        
        # Thermodynamics constants
        Rgas J/(mol*K) = 8.314 J/(mol*K)
        Temp K = 310.0 K
        Farado C/mol = 96485.0 C/mol
        
        # Synaptic efficacy parameters
        tau_star ms = 278.318 ms 
        tau_syn ms = 70000.0 ms
        th_p real = 1.1  * 10**(-21)    # LTP Threshold
        th_d real = 0.4 * 10**(-21)     # LTD Threshold
        p_rate real = 216.2             # Potentiation rate
        d_rate real = 101.5             # Depression rate
        
        # Constant external input current
        I_e pA = 0 pA


    internals:
       # Inline calculations for each synapse
        RefractoryCounts integer = steps(t_ref) # refractory time in steps 
        
        # AMPA initialization
        inline exact_integration_adjustment_AMPA real = ( ( 1 / AMPA_Tau_2 ) - ( 1 / AMPA_Tau_1 ) ) * ms
        inline t_peak_AMPA ms = ( AMPA_Tau_2 * AMPA_Tau_1 ) * ln( AMPA_Tau_2 / AMPA_Tau_1 ) / ( AMPA_Tau_2 - AMPA_Tau_1 )
        inline normalisation_factor_AMPA real = 1 / ( exp( -t_peak_AMPA / AMPA_Tau_1 ) - exp( -t_peak_AMPA / AMPA_Tau_2 ) )
        #inline AMPAInitialValue real = AMPA_g_peak * normalisation_factor_AMPA * exact_integration_adjustment_AMPA
        inline AMPAInitialValue real = normalisation_factor_AMPA * exact_integration_adjustment_AMPA
        inline g_pot nS = (g_bool)? AMPA_g_peak : 2 * AMPA_g_peak
        inline g_dep nS = (g_bool)? 0.5 * AMPA_g_peak : AMPA_g_peak

        # NMDA initialization
        inline exact_integration_adjustment_NMDA real = ( ( 1 / NMDA_Tau_2 ) - ( 1 / NMDA_Tau_1 ) ) * ms
        inline t_peak_NMDA ms = ( NMDA_Tau_2 * NMDA_Tau_1 ) * ln( NMDA_Tau_2 / NMDA_Tau_1 ) / ( NMDA_Tau_2 - NMDA_Tau_1 )
        inline normalisation_factor_NMDA real = 1 / ( exp( -t_peak_NMDA / NMDA_Tau_1 ) - exp( -t_peak_NMDA / NMDA_Tau_2 ) )
        inline NMDAInitialValue real = NMDA_g_peak * normalisation_factor_NMDA * exact_integration_adjustment_NMDA
        
        # VDCC initialization
        inline gca_ba_VDCC real = ( gca_bar_VDCC * 4 * 3.14 * ( ( 3 * volume_CR / ( 4 * 3.14 ) ) )**(2/3) ) / nS
        inline m_VDCC_initialvalue real = 1 / ( 1 + exp( ( ( vhm_VDCC - ljp_VDCC ) - ( ( g_NaL * E_Na + g_KL * E_K ) / ( g_NaL + g_KL ) ) ) / km_VDCC ) )
        inline h_VDCC_initialvalue real = 1 / ( 1 + exp( ( ( vhh_VDCC - ljp_VDCC ) - ( ( g_NaL * E_Na + g_KL * E_K ) / ( g_NaL + g_KL ) ) ) / kh_VDCC ) )
        

    input:
        AMPA <- spike
        NMDA <- spike
        I_EXC <- spike
        I_stim pA <- continuous


    output:
        spike


    update:
       loga = ln( cao_CR / cai_CR )
       cai_CR = max(cai_CR, min_ca_CR)
       integrate_odes()
       cai_CR = max(cai_CR, min_ca_CR)  # Prevent cai_CR from dropping below min_ca_CR
       cstel = cstar * (um**3/mol)
       if r > 0:
           r =  r - 1
       else: 
           g_spike = false
            
           if V_m > Theta:
               Theta = E_Na + 20 * mV
               V_m = E_Na 
               r = RefractoryCounts
               g_spike = true
               emit_spike()
           

    function Hfun(cStel real, th real) real:
        if cStel > th:
            return 1.0
        else:
            return 0.0
