neuron iaf_cond_alpha:
    state:
        r integer = 0                # Counts refractory period ticks
        V_m mV = E_L                 # Membrane potential
        Ca_in mol/um**3 = Ca_rest    # Intracellular calcium concentration

        # Synaptic conductances
        g_AMPA real = 0
        g_NMDA real = 0
        g_AMPA$ real = AMPAInitialValue
        g_NMDA$ real = NMDAInitialValue

        # VDCC gating variables
        m_VDCC real = m_VDCC_initialvalue
        h_VDCC real = h_VDCC_initialvalue

    equations:
        # Kernels
        kernel g_exc = (e/tau_syn_exc) * t * exp(-t/tau_syn_exc)
        
        kernel g_AMPA' = g_AMPA$ - g_AMPA / AMPA_Tau_2,
               g_AMPA$' = -g_AMPA$ / AMPA_Tau_1
               
        kernel g_NMDA' = g_NMDA$ - g_NMDA / NMDA_Tau_2,
               g_NMDA$' = -g_NMDA$ / NMDA_Tau_1
    
        # External excitation current
        inline I_exc pA = convolve(g_exc, exc_spikes) * nS * (V_m - E_exc) # Artificial excitatory current
        
        # VDCC current
        inline m_inf_VDCC real = 1 / (1 + exp((VDCC_V_hm - V_m) / VDCC_k_m))      # Steady state activation variable m
        inline h_inf_VDCC real = 1 / (1 + exp((VDCC_V_hh - V_m) / VDCC_k_h))      # Steady state inactivation variable h
        inline g_VDCC real = VDCC_g_peak * m_VDCC**2 * h_VDCC
        m_VDCC' = (m_inf_VDCC - m_VDCC) / VDCC_tau_m
        h_VDCC' = (h_inf_VDCC - h_VDCC) / VDCC_tau_h
        inline VDCC_E_rev mV = (Rgas * Temp / (2 * Farado)) * (Ca_out / Ca_in)  # Nernst equation for VDCC reverse potential
        recordable inline I_VDCC pA = -g_VDCC * nS * (V_m - VDCC_E_rev)           # Calcium current through VDCC
        
        # AMPA current
        recordable inline I_AMPA pA = -convolve(g_AMPA, AMPA) * nS * (V_m - AMPA_E_rev)
        
        # NMDA current
        inline Mg_gate real = 1 / (1 + exp(-1 * V_m / NMDA_Mg_slope) * (Mg_out / NMDA_Mg_scale)) # Sigmoidal that depends on the membrane potential
        recordable inline I_NMDA pA = -convolve(g_NMDA, NMDA) * nS * (V_m - NMDA_E_rev) * Mg_gate
        
        # Leak current
        inline I_leak pA = g_L * (V_m - E_L)
        
        # Calcium dynamics
        inline NMDA_Ca_fraction real = (4 * Ca_out) / (4 * Ca_out + (1/1.38) * 120 * (mol/um**3)) * 0.6 # TODO: Justify
        recordable inline I_NMDA_Ca pA = NMDA_Ca_fraction * I_NMDA # NMDA mediated calcium current
        Ca_in' = ( I_NMDA_Ca + I_VDCC ) * ( Ca_eta / ( 2 * Ca_X_volume * Farado ) ) - ( ( Ca_in - Ca_rest ) / Ca_tau )

        # Membrane potential dynamics
        inline I_total pA = I_AMPA + I_NMDA + I_VDCC + I_exc
        V_m' = (-I_leak - I_total + I_stim) / C_m

    parameters:
        # Neuron parameters
        V_th mV = -55 mV         # Threshold potential
        V_reset mV = -60 mV      # Reset potential
        t_ref ms = 2 ms          # Refractory period
        g_L nS = 16.6667 nS      # Leak conductance
        C_m pF = 250 pF          # Membrane capacitance
        E_L mV = -70 mV          # Leak reversal potential
        
        # Artificial excitation parameters
        E_exc mV = 0 mV          # Excitatory reversal potentials
        tau_syn_exc ms = 0.2 ms  # Synaptic time constant of excitatory synapse

        # AMPA parameters
        AMPA_Tau_1 ms = 0.2 ms        # Tau rise, dual-exponential conductance profile
        AMPA_Tau_2 ms = 1.7 ms        # Tau decay, IMPORTANT: tau_1 < tau_2
        AMPA_g_peak nS = 0.1 nS       # AMPA peak conductance
        AMPA_E_rev mV = 0 mV          # AMPA reverse potential

        # NMDA parameters
        NMDA_Tau_1 ms = 0.29 ms                      # Tau rise, dual-exponential conductance profile
        NMDA_Tau_2 ms = 70 ms                        # Tau decay, IMPORTANT: tau_1 < tau_2
        NMDA_g_peak nS = 0.55 nS                     # NMDA peak conductance
        NMDA_E_rev mV = -3 mV                        # NMDA reverse potential
        NMDA_Mg_scale mol/um**3 = 2.552 mol/um**3    # Scale of the Mg block (Vargas-Caballero and Robinson 2003)
        NMDA_Mg_slope mV = 0.072 mV                  # Slope of the Mg block (Vargas-Caballero and Robinson 2003)
        # NMDA_Ca_fraction real = 0.1                # Fraction of NMDA current carried by calcium (now made dynamic)
        
        # Mangesium parameters
        Mg_out mol/um**3 = 1 mol/um**3               # Extracellular magnesium concentration
        
        # VDCC parameters
        VDCC_g_bar nS/um**2 = 0.0744 nS/um**2 # Density spines: 20 um-2 (Sabatini 2000), unitary conductance VGCC 3.72 pS (Bartol 2015)
        VDCC_V_hm mV = -5.9 mV                # Half-maximum activation voltage for VDCC, Magee and Johnston 1995 (corrected for m*m)
        VDCC_k_m mV = 9.5 mV                  # Slope factor for VDCC activation, Magee and Johnston 1995 (corrected for m*m)
        VDCC_tau_m ms = 1 ms                  # Time constant activation variable m, TODO: It's a guess
        VDCC_V_hh mV = -39 mV                 # Half-maximum inactivation voltage for VDCC, Magee and Johnston 1995
        VDCC_k_h mV = -9.2 mV                 # Slope factor for VDCC inactivation, Magee and Johnston 1995
        VDCC_tau_h ms = 27 ms                 # Time constant inactivation variable h

        # Calcium parameters
        Ca_rest mol/um**3 = 0.07e-3 mol/um**3  # Sabatini et al 2002: 70+-29 nM, per AP: 1.1 (0.6-8.2) uM = 1100 e-6 mM = 1100 nM
        Ca_out mol/um**3 = 2.0e-3 mol/um**3    # Extracellular calcium concentration in slices
        Ca_tau ms = 12.0 ms                    # Time constant of calcium transients Sabatini et al.
        Ca_X_volume um**3 = 0.087 um**3        # Spine volume
        Ca_eta real = 0.04                     # Percent of free calcium (not buffered), Sabatini et al 2002: kappa_e = 24+-11 (also 14 (2-31) or 22 (18-33))

        # Thermodynamic constants
        Rgas J/(mol*K) = 8.314 J/(mol*K)
        Temp K = 310.0 K
        Farado C/mol = 96485.0e-15 C/mol

    internals:
        # AMPA initialization
        inline exact_integration_adjustment_AMPA real = ( ( 1 / AMPA_Tau_2 ) - ( 1 / AMPA_Tau_1 ) ) * ms
        inline t_peak_AMPA ms = ( AMPA_Tau_2 * AMPA_Tau_1 ) * ln( AMPA_Tau_2 / AMPA_Tau_1 ) / ( AMPA_Tau_2 - AMPA_Tau_1 )
        inline normalisation_factor_AMPA real = 1 / ( exp( -t_peak_AMPA / AMPA_Tau_1 ) - exp( -t_peak_AMPA / AMPA_Tau_2 ) )
        inline AMPAInitialValue real = AMPA_g_peak * normalisation_factor_AMPA * exact_integration_adjustment_AMPA
        
        # NMDA initialization
        inline exact_integration_adjustment_NMDA real = ( ( 1 / NMDA_Tau_2 ) - ( 1 / NMDA_Tau_1 ) ) * ms
        inline t_peak_NMDA ms = ( NMDA_Tau_2 * NMDA_Tau_1 ) * ln( NMDA_Tau_2 / NMDA_Tau_1 ) / ( NMDA_Tau_2 - NMDA_Tau_1 )
        inline normalisation_factor_NMDA real = 1 / ( exp( -t_peak_NMDA / NMDA_Tau_1 ) - exp( -t_peak_NMDA / NMDA_Tau_2 ) )
        inline NMDAInitialValue real = NMDA_g_peak * normalisation_factor_NMDA * exact_integration_adjustment_NMDA
        
        # VDCC initialization
        inline VDCC_g_peak real = ( VDCC_g_bar * 4 * 3.14 * ( ( 3 * Ca_X_volume / ( 4 * 3.14 ) ) )**(2/3) ) # VDCC conductance peak
        m_VDCC_initialvalue real = 1 / (1 + exp((VDCC_V_hm - V_reset) / VDCC_k_m))
        h_VDCC_initialvalue real = 1 / (1 + exp((VDCC_V_hh - V_reset) / VDCC_k_h))

    input:
        exc_spikes <- spike
        AMPA <- spike
        NMDA <- spike
        I_stim pA <- continuous

    output:
        spike

    update:
        integrate_odes()

        if r > 0:  # Refractory period
            r = r - 1
            V_m = V_reset
        elif V_m >= V_th:  # Spike generation
            emit_spike()
            r = steps(t_ref)
            V_m = V_reset

